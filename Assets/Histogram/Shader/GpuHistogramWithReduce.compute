#include "../../ShaderUtils/Math.compute"

#pragma kernel CSHistogram
#pragma kernel CSReduce

#define BLOCK_AMOUNT      8
#define BLOCK_DIMEN       uint3(BLOCK_AMOUNT, BLOCK_AMOUNT, BLOCK_AMOUNT)
#define BLOCK_SIZE        (BLOCK_AMOUNT * BLOCK_AMOUNT * BLOCK_AMOUNT)  
#define GRID_SIZE         (g_gridDimen.x * g_gridDimen.y * g_gridDimen.z) 

uint3           g_gridDimen;
uint            g_size;
uint            g_binSize;
uint            g_offset;

Buffer<int>     g_in;
RWBuffer<int>   g_out;

groupshared int g_sharedData[BLOCK_SIZE];
groupshared int g_sharedHisto[BLOCK_SIZE];

[numthreads(BLOCK_DIMEN.x, BLOCK_DIMEN.y, BLOCK_DIMEN.z)]
void CSHistogram( uint3 Gid  : SV_GroupID, 
                  uint3 DTid : SV_DispatchThreadID, 
                  uint3 GTid : SV_GroupThreadID,
                  uint  GI   : SV_GroupIndex )
{
    uint tid = to1D(GTid, BLOCK_DIMEN);
    uint gid = to1D(Gid, g_gridDimen);
    uint globalId = tid + gid * BLOCK_SIZE;
    
    g_sharedHisto[tid] = 0;
    if (globalId < g_size) g_sharedData[tid] = g_in[globalId];
    GroupMemoryBarrierWithGroupSync();
    
    if (globalId < g_size) InterlockedAdd(g_sharedHisto[uint(abs(g_sharedData[tid]) % g_binSize)], 1);
    GroupMemoryBarrierWithGroupSync();
    
    g_out[tid * GRID_SIZE + gid] = g_sharedHisto[tid];
}

[numthreads(BLOCK_DIMEN.x, BLOCK_DIMEN.y, BLOCK_DIMEN.z)]
void CSReduce( uint3 Gid  : SV_GroupID, 
               uint3 DTid : SV_DispatchThreadID, 
               uint3 GTid : SV_GroupThreadID,
               uint  GI   : SV_GroupIndex )
{
    uint totalSize = g_offset * g_binSize;
    uint tid = to1D(GTid, BLOCK_DIMEN);
    uint gid = to1D(Gid, g_gridDimen);
    uint globalId = tid + gid * g_offset;
    uint startId = to1D(uint3(0,Gid.y,0), g_gridDimen) * g_offset;
    
    g_sharedHisto[tid] = (globalId < startId + g_size)
        ? g_in[globalId]
        : int(0);
    GroupMemoryBarrierWithGroupSync();
    
    for (uint s = (min(BLOCK_SIZE, g_size) >> 1); s > 0; s >>= 1) {
        uint nextTid = (tid + s);
        if (tid < s && nextTid < g_size) 
            g_sharedHisto[tid] += g_sharedHisto[nextTid];
        GroupMemoryBarrierWithGroupSync();
    }
    
   if (tid == 0) g_out[gid] = g_sharedHisto[0];
}